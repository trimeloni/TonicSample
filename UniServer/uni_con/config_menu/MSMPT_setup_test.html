<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.01//EN" "http://www.w3.org/TR/html4/strict.dtd">
<html>
<head>
<title>Untitled Document</title>

<meta http-equiv="Content-Type" content="text/html; charset=UTF-8" />

<link rel="stylesheet" type="text/css" href="../css/main_style.css" media="screen" />
<script type="text/vbscript" src="../includes/core_config_inc.vbs"></script>
<script type="text/vbscript" src="../includes/core_functions_inc.vbs"></script>
<script type="text/vbscript" src="../lang/lang.vbs"></script>

<script type="text/vbscript">
 'MPG 18-4-2011 V1.0
 '--Globals
 Dim id_account

 Set objShell = CreateObject("WScript.Shell")

'******************************************************************************
'Window_onLoad runs any time the HTA starts up or is refreshed
'******************************************************************************
Sub Window_OnLoad
 us_init()             'Check servers have moved and update accordingly

 'Global menu object selects
 set id_account   = msmpt_form.id_msmpt_account       'General - common menu obj

 initial_text()                  'Set button and side text from lang file
 populate_menu() 'Add items to menu

End sub

'******************************************************************************
'Set initial button text. Text from language configuration file
'Set side text from lang file
'******************************************************************************
Function initial_text()

  legi_msmpt_account.InnerHtml     = lang_msmpt_account_frame     'Select Default Account
  legi_msmpt_test_email.InnerHtml  = lang_msmpt_email_test_frame  'Send a test E mail

  td_msmpt_email_to.InnerHtml     = lang_msmpt_email_to           'Email To
  td_msmpt_subject.InnerHtml      = lang_msmpt_subject            'Subject
  td_msmpt_message.InnerHtml      = lang_msmpt_message            'Message  

  '--Input boxes
  ip_msmpt_email_to.value    = lang_msmpt_email_to_user_input   'mexxx@tiscali.co.uk
  ip_msmpt_subject.value     = lang_msmpt_subject_user_input    'UniServer Test
  ip_msmpt_message.value     = lang_msmpt_message_user_input    'Test Message 1. Delete and use your own!

  bti_msmpt_send_mail.value       = lang_msmpt_send_mail_bt       'Send Mail

End Function
'------------------------------------------------------------------------------

'=== Populate menu ===========================================================
Function populate_menu()

  Dim file_array,strLine,strNew,strNew2

 '=== Get values from configuration file
 file_array = us_get_file_as_array(USF_MSMTP_INI)     'Read MSMTP config file

  For Each strLine in file_array                      'Scan array
    strLine = trim(strLine)                           'Clean each line
    strLine = Replace(strLine," ","")                 'Remove spaces

    If Instr(1,strLine,"accountdefault:") = 1 Then        'Look for string
        strNew  = Replace(strLine,"accountdefault:","")   'Rmove.
    End If                                                'strNew contains default account
  Next

  '--- Set Default account, first entery in select
  call us_select_add_item(id_account ,strNew,strNew)      'Set default

  '---Scan array and populate select menu with all accounts
  For Each strLine in file_array                      'Scan array
    strLine = trim(strLine)                           'Clean each line
    strLine = Replace(strLine," ","")                 'Remove spaces

    If Not Instr(1,strLine,"accountdefault:") = 1 Then 'Skip if "account default :"

      If Instr(1,strLine,"account") = 1 Then          'Look for "account"
          strNew2  = Replace(strLine,"account","")    'Rmove. Save account info
            If Not (strNew2 = strNew) Then            'Skip if default account
              call us_select_add_item(id_account ,strNew2,strNew2) 'Add to drop-down
            End If
      End If 

    End If                                                

  Next

End Function
'======================================================= End Populate menu ===

'=== Change Default account ===================================================
'Function called when user makes a new selection (changes)
'Configuration file updated to new selection.
Function f_msmpt_account()
 Dim file_array,strLine,i,str
 Dim selected

 id_account.blur                   'Remove focus 
 selected = id_account.value       'Get value selected by user

 file_array = us_get_file_as_array(USF_MSMTP_INI)     'Read MSMTP config file

 For i = 0 To UBound(file_array)                        'Scan all lines in array
   strLine = trim(file_array(i))                        'Clean each line
   strLine = Replace(strLine," ","")                    'Remove spaces

   If Instr(1,strLine,"accountdefault:") = 1 Then       'Look for "account default :" line
     file_array(i) = "account default : "  &  selected  'Replace array content with new line
   End If
 Next

   call us_save_array_to_file(file_array,USF_MSMTP_INI) 'Save array 

 'MsgBox selected ' Test

End Function
'=============================================== End Change Default account ===

'=== Send Test Email ==========================================================
Function btf_msmpt_send_mail()
Dim email_address,subject,content,str,str1,cmd
Dim cmd1,cmd2,cmd3,objShell,FS

 email_address = ip_msmpt_email_to.value    'Address of recipient
 subject       = ip_msmpt_subject.value     'Subject header
 content       = ip_msmpt_message.value     'Message to send

 '--Create subject and mesage to send. Save to a temp file
 str = "Subject: " & subject & vbCRLF & vbCRLF  & content     'String to send
 Call us_writefile_str(US_TMP & "\temp.mail", str)            'Save string to temp file

 '--Create a string save to a batch file
 str1 = ""
 str1 = str1 & "@ECHO OFF" & vbCRLF 
 str1 = str1 &  "REM Send email from command line via SMTP using MSMTP" & vbCRLF 
 str1 = str1 &  "REM The ECHO. line after ECHO Subject: is important and must be included" & vbCRLF 
 str1 = str1 &  USF_MSMTP_EXE & " --file=" & USF_MSMTP_INI & " " & email_address & " -t < " & US_TMP & "\temp.mail" & vbCRLF 
 str1 = str1 &  "DEL " &  US_TMP & "\mail_temp.bat" & vbCRLF 

 Call us_writefile_str(US_TMP & "\mail_temp.bat", str1)  'Save string to file

 '--Example line
 'cmd ="msmtp --file=C:\Coral_8_0_0\UniServer\msmtp\msmtprc.ini user_name@target_email_address -t < %TEMP%\temp.mail"

 '--Send mail using MSMTP
  Set objShell = CreateObject("WScript.Shell")   'Create object
  objShell.Run US_TMP & "\mail_temp.bat",0,True  'Run batch file hidden

 '--Clean-up delete batch file
  'Create file object
  set FS=CreateObject("Scripting.FileSystemObject")

   'Remove file
   If FS.FileExists(US_TMP & "\mail_temp.bat") Then
    FS.DeleteFile US_TMP & "\mail_temp.bat"
   End If 

 '--User feedback
 msgbox lang_msmpt_test_str,,lang_msmpt_test_title

 bti_msmpt_send_mail.blur                   'Remove focus 
End Function
'====================================================== End Send Test Email ===

</script>

<style type="text/css">
/*****************************************/
table{
 width:100%;
}
td{
  padding:0px; 
  font-size:11px;
  font-family:Verdana;
}

textarea{
 font-size:11px;
 font-family:Verdana;
 width:97%;
 height:50px;
 background-color: #D1D1F5;
}


input{
 font-size:11px;
 font-family:Verdana;
 width:97%;

 background-color: #D1D1F5;
}

select{
 background-color: #D1D1F5;
  font-size:11px;
  font-family:Verdana;
}

button{
 font-size:11px;
 font-family:Verdana;
}
/*****************************************/
</style>
</head>

<body>

<table width="100%" border="0" cellspacing="2">
 <tr><td>
 <fieldset>
 <legend id="legi_msmpt_account" >Select Default Account</legend> 

   <form id="msmpt_form"  style="margin:0px;padding:0px">
   <select  onchange="f_msmpt_account()" id="id_msmpt_account" style="width:100%;"></select>
   </form>

  </fieldset>
 </td></tr>

 <tr><td>

 <fieldset>
 <legend id="legi_msmpt_test_email" >Send a test E-mail</legend> 

   <table width="100%" border="0" cellspacing="4">
    <tr>
      <td width="60%"><input  id="ip_msmpt_email_to" type="text"></td>
      <td id="td_msmpt_email_to" >Email To</td>
    </tr>
    <tr>
      <td><input  id="ip_msmpt_subject" type="text"></td>
      <td id="td_msmpt_subject" >Subject</td>
    </tr>

    <tr>
      <td><textarea id="ip_msmpt_message" ROWS=2 > </textarea></td>
      <td ><p id="td_msmpt_message"  >Message</p>
        <button id="bti_msmpt_send_mail" onclick="btf_msmpt_send_mail()" >Send Mail</button> </td>
    </tr>

   </table>

  </fieldset>

 </td></tr>

  </table>

</body>
</html>
