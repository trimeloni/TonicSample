<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.01//EN" "http://www.w3.org/TR/html4/strict.dtd">
<html>
 <head>

  <script type="text/jscript" >
  // Resize and hide window. This script requires placing just below head tag.
  // Note: This js is faster than vbs hence avoids flicker.
  //       Window is hidden by moving it off screen.  

   var hta_width  = 406;                                // Set Application width
   var hta_height = 282;                                // Set Application height
   window.resizeTo(hta_width,hta_height);               // Resize window
   var hta_left = (window.screen.width-hta_width)/2;    // New position centered
   var hta_top  = (window.screen.height-hta_height)/2;  // New position centered
   window.moveTo(-2000,-2000);                          // Hide window! Move off screen
   setTimeout('show(hta_left,hta_top)',200);            // Schedule (ms) show function

   function show(hta_left,hta_top) {                    // Show Window by moving 
      window.moveTo(hta_left,hta_top);                  // it into view.
   }
  </script>

  <title>Server - Program</title>
 
  <hta:application
   id='coral_server_1'
   applicationname='coral_server_1'
   border="thin"
   icon='./images/coral_server_1.ico'
   maximizeButton="no"
   navigable="yes"
   scroll="no"
   singleinstance="yes"
  />
 
  <meta http-equiv="Content-Type" content="text/html; charset=UTF-8" />

<script type="text/vbscript" src="includes/core_config_inc.vbs"></script>
<script type="text/vbscript" src="includes/core_functions_inc.vbs"></script>
<script type="text/vbscript" src="lang/lang.vbs"></script>

<script type="text/vbscript">
 Option Explicit

'******************************************************************************
'Window_onLoad runs any time the HTA starts up or is refreshed
'******************************************************************************
Sub Window_OnLoad
 Dim refrtime

 document.title = "Server - Program -" & " " & us_get_server_version ' Set new page title

 space_check()         'Check for spaces in path
 us_init()             'Check servers have moved and update accordingly
 us_za_issue()         'Check for ZoneAlarm and warn user if running.
 button_initial_text() 'Set button default text
 update_status()       'Update button status when page has loaded 

 'Check ports are free and passwords set
 call setTimeout("pre_check", 10, "vbscript") 'Allow display then update pre_check()

End sub

'=== Space check ==============================================================
' Legacy suitability check no spaces in path to folder UniServer allowed
' Also avoids Vista's insufficient write permissions in C:\Program Files
Function space_check()
 Dim path
   path = location.pathname    '* Get path to this file

  If InStr(path," ") Then      'Check for space
     Msgbox lang_space_popup_str1,,lang_space_popup_title1
     window.close()            'Close cant start server with space
     exit function             'Nothing else to do
  End If

End Function
'========================================================== End Space check ===

'=== ZoneAlarm Detection ======================================================
' The ZoneAlarm application conflicts with the Apache Server
' producing a blue screen of death when Apache is closed.   
                    
Function us_za_issue()
 Dim path

  If us_process_running("zatray.exe") Then         'Check ZA is running
    Msgbox lang_za_popup_str1,,lang_za_popup_title1  'Yes: Display warning
  End If

End Function
'================================================== End ZoneAlarm Detection ===


'=== Pre Check ================================================================
Function pre_check()
 Dim objShell,apache_port,apache_ssl_port,mysql_port,port_in_use,port_in_use_str 
 Dim details_array,skip,data_str,split_array

 Set objShell = CreateObject("WScript.Shell")
'--- To start servers ports must be free. If these are in use by another
'    application warn user to take corrective action.

  '-- Get current server ports - May have been changed from defaults 
  apache_port     = us_get_apache_port()
  apache_ssl_port = us_get_apache_ssl_port()
  mysql_port      = us_get_mysql_port()

  port_in_use     = false   ' true - display warning message 
  port_in_use_str = ""      ' set warning message to display 
  port_in_use_str =  lang_pre_in_use & vbCRLF & vbCRLF

  '-- Is Apache being used
  if us_port_in_use_ap(apache_port) And us_get_apache_tracker() = "free" Then 
   port_in_use = true                            'port in use set flag 
   details_array   =  us_get_port_user_details(apache_port) 'Get details
   port_in_use_str = port_in_use_str & lang_pre_port_number      & " = " & details_array(0) & vbCRLF
   port_in_use_str = port_in_use_str & lang_pre_pid              & " = " & details_array(1) & vbCRLF
   port_in_use_str = port_in_use_str & lang_pre_process_name     & " = " & details_array(2) & vbCRLF
   port_in_use_str = port_in_use_str & lang_pre_path_to_process  & " = " & details_array(3) & vbCRLF & vbCRLF
  End If

  '-- Is Apache SSL being used
  if us_port_in_use_ap(apache_ssl_port) And us_get_apache_tracker() = "free" Then 
   port_in_use = true                            'port in use set flag 
   details_array   =  us_get_port_user_details(apache_ssl_port) 'Get details
   port_in_use_str = port_in_use_str & lang_pre_port_number      & " = " & details_array(0) & vbCRLF
   port_in_use_str = port_in_use_str & lang_pre_pid              & " = " & details_array(1) & vbCRLF
   port_in_use_str = port_in_use_str & lang_pre_process_name     & " = " & details_array(2) & vbCRLF
   port_in_use_str = port_in_use_str & lang_pre_path_to_process  & " = " & details_array(3) & vbCRLF & vbCRLF
  End If

  '-- Is MySQL being used
  if us_port_in_use_my(mysql_port) And us_get_mysql_tracker() = "free" Then 
   port_in_use = true                            'port in use set flag 
   details_array   =  us_get_port_user_details(mysql_port) 'Get details
   port_in_use_str = port_in_use_str & lang_pre_port_number      & " = " & details_array(0) & vbCRLF
   port_in_use_str = port_in_use_str & lang_pre_pid              & " = " & details_array(1) & vbCRLF
   port_in_use_str = port_in_use_str & lang_pre_process_name     & " = " & details_array(2) & vbCRLF
   port_in_use_str = port_in_use_str & lang_pre_path_to_process  & " = " & details_array(3) & vbCRLF & vbCRLF
  End If

   port_in_use_str = port_in_use_str & lang_pre_popup_str1  'Add solution text

  If port_in_use Then                                  'Display warning message 
     Msgbox port_in_use_str,,lang_pre_popup_title1     'Ports in use pop-up box

     'Disable buttons
      bti_start_apache.Disabled      = True   'disable button 
      bti_start_mysql.Disabled       = True   'disable button 
      bti_start_both.Disabled        = True   'disable button 

     Set objShell = Nothing
     exit function
  End If

  '=== Nag section ===
  ' If a port in use no point in nagging since the following section
  ' Requires a working server 

  If Not port_in_use Then             'Stand a good chance servers will run 

   If us_my_ini_get(USF_TRACKER,"NAG","nag_user") = "true" Then 'Is nag user set
   skip = false                                                 'Skip tracker. Provides option to disable later


      '-- Check passwords have been changed. If defaults detected nag user

      '--- MySQL password
      If  us_get_mysql_password() = "root" Then            'Is default 
        '-- Ask if they wish to change MySQL password   
        If MsgBox(lang_pre_popup_str2,4,lang_pre_popup_title2) = vbYes Then
            objShell.Run (US_UNI_CON & "\scripts\Set_new_mysql_password.hta"),,true 'Display MySQL Password change window
        Else
            skip = true 'User skipped setting a password
        End If
      End If

      '--- Option to disable nagging
      ' User skipped one or more hence provide option to disable nagging 
      If skip Then
        'Ask if they wish to disable nagging in config_tracker.ini   
        If MsgBox(lang_pre_popup_str4,4,lang_pre_popup_title4) = vbYes Then
          call us_my_ini_set(USF_TRACKER,"NAG","nag_user","false") 'Diable nagging in config file
        End If
      End If 'end if skip


   End If '--nag_user
  End If '--Not port_in_use
  Set objShell = Nothing
End Function
'============================================================ End Pre Check ===

'******************************************************************************
'Set initial button text. Text from language configuration file
'Set initial button status. Assumes server not running
'******************************************************************************
Function button_initial_text()
 legi_start_stop_servers.InnerHtml = lang_legend_start_stop_servers
  bti_start_apache.value           = lang_start_apache
  bti_start_mysql.value            = lang_start_mysql
  bti_start_both.value             = lang_start_both

 legi_server_utilities.InnerHtml   = lang_server_utilities
  bti_mysql_console.value          = lang_mysql_console
  bti_server_status.value          = lang_server_status
  bti_apache_syntax_check.value    = lang_apache_syntax_check
  bti_server_configuration.value   = lang_server_configuration
  bti_multi_servers.value          = lang_multi_servers
  bti_about.value                  = lang_about

  bti_view_www.value               = lang_view_www
  bti_view_ssl.value               = lang_view_ssl
  bti_view_phpinfo.value           = lang_view_php_info
  bti_view_phpmyadmin.value        = lang_phpmyadmin
  bti_server_documentation.value   = lang_server_documentation

  imgi_apache.src = "images/red.gif"  'display Apache stop
  imgi_mysql.src  = "images/red.gif"  'display MySQL stop

  bti_mysql_console.Disabled   = True   'disable button 
  bti_view_www.Disabled        = True   'disable button 
  bti_view_ssl.Disabled        = True   'disable button 
  bti_view_phpinfo.Disabled    = True   'disable button 
  bti_view_phpmyadmin.Disabled = True   'disable button 

  populate_language_menu()              'Add items to drop down menu

End Function
'------------------------------------------------------------------------------

'******************************************************************************
'Populate Language menu from files in language folder
'******************************************************************************
Function populate_language_menu()
 Dim objFSO,objFolder,colFiles,objFile,id

 set id  = seli_form_lang.seli_select_lang                   'Create id select object

 call us_select_add_item(id ,"Dummy1",lang_select_language)        'Add first item
 call us_select_add_item(id ,"Dummy2",lang_select_language_spacer) 'Add second item

 '-- Read all files and add to list
 Set objFSO = CreateObject("Scripting.FileSystemObject")    'Create file object
 Set objFolder = objFSO.GetFolder(US_UNI_CON_LANG)            'Create object to folder

 Set colFiles = objFolder.Files                             'Get list af all files
 For Each objFile in colFiles                               'Scan list of files
   If Not objFile.Name = "lang.vbs" Then                    'Exclude master language file
     call us_select_add_item(id,objFile.Name,Left(objFile.Name, Len(objFile.Name) - 4))
   End If
 Next
 call us_select_add_item(id ,"Dummy2",lang_select_language_spacer) 'Add last item

 set id        = Nothing 
 Set objFSO    = Nothing 
 Set objFolder = Nothing 
End Function
'------------------------------------------------------------------------------

'******************************************************************************
'Update Language file
'******************************************************************************
'Gets selected language. Replaces language file with selected language file
'Reload page to reflect new language change

Function fsel_update_lang()
 Dim file_name,selected_file,lang_file,objFSO
 Const OverwriteExisting = TRUE

 seli_form_lang.seli_select_lang.blur                 'Remove focus 
 file_name = seli_form_lang.seli_select_lang.value    'Get selection

 If not file_name = "Dummy1" and not file_name = "Dummy2" Then    'Skip if not language selection

   '-- Set language in configuration file
   call us_my_ini_set(USF_TRACKER,"LANGUAGE","selected_lang",Left(file_name, Len(file_name) - 4))   'Set a value in .ini file  

   '-- Switch in language file
   selected_file = US_UNI_CON_LANG & "\" & file_name   'Buld full path
   lang_file     = US_UNI_CON_LANG & "\" & "lang.vbs"  'File to replace

   Set objFSO = CreateObject("Scripting.FileSystemObject")      'Create file object
   objFSO.CopyFile selected_file , lang_file, OverwriteExisting 'Replace lang file
   Set objFSO = Nothing 
   Location.Reload(True)                                        'Reload page
   us_hta_delay("1000")                                         'Delay in ms now update UniTray
   us_unitray_update("Change")                                  'Check for UniTray and change status
 Else
  seli_form_lang.seli_select_lang.value ="Dummy1"      'Re-set initial value displayed to user
 End If
End Function
'------------------------------------------------------------------------------


'******************************************************************************
'Update status of each button. Uses real time information
'******************************************************************************
Function update_status()
 Dim  apache_tracker,mysql_tracker,AP,MY,APS,MYS,SSL

 '== Get server current status from trackers (minimum file read)
 apache_tracker  = us_get_apache_tracker()    ' Apache tracker
 mysql_tracker   = us_get_mysql_tracker()     ' MySQL tracker

 '== Server status shortcuts (reducees processing)
 AP  = us_apache_running()                    ' Apache running shortcut
 MY  = us_mysql_running()                     ' MySQL  running shortcut
 APS = us_is_apache_installed_as_a_service()  ' Installed service
 MYS = us_is_mysql_installed_as_a_service()   ' Installed service
 SSL = us_ssl_enabled()                       ' SSL Enabled

'=== PROGRAM OR SERVICE ===

 '=== APACHE button --- 
 If APS Then                                   '==Installed as service
   bti_start_apache.Disabled = True            'Disable button 
 ELSE                                          '==Installed as program
   If Not AP And apache_tracker = "free" Then
     bti_start_apache.Disabled = False           'Enable button 
     bti_start_apache.value = lang_start_apache  'Change text to start
   Else
    If AP And apache_tracker = "program" Then
     bti_start_apache.Disabled = False           'Enable button 
     bti_start_apache.value = lang_stop_apache   'Change text to stop
    End If
   End If
 End If '===APS

 '=== MySQL button --- 
 If MYS Then                                   '==Installed as service
   bti_start_mysql.Disabled  = True            'Disable button 
 ELSE                                          '==Installed as program
   If Not MY And mysql_tracker = "free" Then
     bti_start_mysql.Disabled = False          'Enable button 
     bti_start_mysql.value = lang_start_mysql  'Change text to start
   Else
    If MY And mysql_tracker = "program" Then
     bti_start_mysql.Disabled = False          'Enable button 
     bti_start_mysql.value = lang_stop_mysql   'Change text to stop
    End If
   End If
 End If '===MYS

 '=== Both buttons --- 
 If APS Or MYS Then                            '== One Installed as service
   bti_start_both.Disabled  = True             'Disable button 
 ELSE                                          '==Installed as program
   bti_start_both.Disabled = True              'Disable button 
   If Not AP And apache_tracker = "free" And Not MY And mysql_tracker = "free" Then
     bti_start_both.Disabled = False              'Enable button 
     bti_start_both.value = lang_start_both       'Change text to start
   Else
    If AP And apache_tracker = "program" And MY And mysql_tracker = "program" Then
     bti_start_both.Disabled = False              'Enable button 
     bti_start_both.value = lang_stop_both        'Change text to stop
    End If
   End If
 End If '===APS Or MYS

 '=== INDICATORS =====

 '== Apache indicator
 If AP Then                              ' Apache running
   imgi_apache.src = "images/green.gif"  ' display Apache running
 Else                                    ' Apache not running
   imgi_apache.src = "images/red.gif"    ' display Apache stopped
 End If

 '== MySQL indicator
 If MY Then                              ' MySQL running
   imgi_mysql.src = "images/green.gif"   ' display MySQL running
 Else                                    ' MySQL not running
   imgi_mysql.src = "images/red.gif"     ' display MySQL stopped
 End If

 '=== BUTTON GROUPS ======

 '== Root www and Apanel 
 If AP Then
  bti_view_www.Disabled      = False     'Enable Root WWW 
  bti_view_phpinfo.Disabled  = False     'Enable phpInfo button 
 Else
  bti_view_www.Disabled      = True      'Disable Root WWW 
  bti_view_phpinfo.Disabled   = True     'Disable phpInfo button 
 End If

 '== Root ssl
 If AP And SSL Then
  bti_view_ssl.Disabled      = False    'Enable Root SSL buton 
 Else
  bti_view_ssl.Disabled      = True     'Disable Root SSL button 
 End If

 '== MySQL Command prompt
 If MY Then
  imgi_mysql.src = "images/green.gif"      'display MySQL running
  bti_mysql_console.Disabled      = False  'Enable MySQl Command prompt 
 Else
  imgi_mysql.src = "images/red.gif"        'display MySQL stopped
  bti_mysql_console.Disabled      = True   'Disable MySQl Command prompt 
 End If

 '== phpMyAdmin
 If MY And AP Then
  bti_view_phpmyadmin.Disabled  = False     'Enable phpMyAdmin 
 Else
  bti_view_phpmyadmin.Disabled  = True      'Disable phpMyAdmin 
 End If

 '== Multi Servers
 If MY Or AP Then
  bti_multi_servers.Disabled  = True     'Disable Multi Servers 
 Else
  bti_multi_servers.Disabled  = False    'Enable Multi Servers 
 End If

End Function
'------------------------------------------------------------------------------

'******************************************************************************
' Start/Stop Apache as a program - Toggle button
'******************************************************************************
Function  btf_start_apache()
  Dim safety_count,safety_count_max                    'Counter 
  safety_count_max  = 15                               'Time out

  bti_start_apache.Disabled = True                     'disable button 
  bti_start_both.Disabled   = True                     'disable button 
  imgi_apache.src="images/wait16.gif"                  'display busy image
 

   if bti_start_apache.value = lang_start_apache then  'If start button   
      us_start_apache_program()                        'Start Apache
 
     '--Saftey Loop---
     safety_count = 0                                    'Reset counter
     Do                                                  'Wait for Apache to start
       us_hta_delay("1000")                              'Delay in ms
       safety_count = safety_count +1                    'Increment
       If safety_count >= safety_count_max Then          'Limit reached bail out                          
          bti_start_apache.Disabled = False              'enable button 
          bti_start_both.Disabled   = False              'enable button
          imgi_apache.src="images/red.gif"               'display stop image
          Msgbox lang_program_str1,,lang_program_title1  'Inform user
          Exit Function                                  'Bail out
       End If
      Loop While Not us_apache_running()                 'Check Apache status
     '-- End Saftey Loop---

      us_hta_delay("2000")                             'Delay in ms
      us_redirect("start") 'Check user wants to display page. Display accordingly

   Else                                                'Is stop button
      us_stop_apache_program()                         'Stop Apache

     '--Saftey Loop---
     safety_count = 0                                    'Reset counter
     Do                                                  'Wait for Apache to stop
       us_hta_delay("1000")                              'Delay in ms
       safety_count = safety_count +1                    'Increment
       If safety_count >= safety_count_max Then          'Limit reached bail out                          
          bti_start_apache.Disabled = False              'enable button 
          bti_start_both.Disabled   = False              'enable button
          imgi_apache.src="images/green.gif"             'display start image
          Msgbox lang_program_str2,,lang_program_title2  'Inform user
          Exit Function                                  'Bail out
       End If
      Loop While us_apache_running()                     'Check Apache status
     '-- End Saftey Loop---

   End if

   update_status()                                'Update button status
   us_hta_delay("1000")                           'Delay in ms now update UniTray
   us_unitray_update("Refresh")                   'Check for UniTray and refresh status

  bti_start_apache.Disabled = false               'Enable button 
  bti_start_apache.blur                           'Remove focus and double line 
End Function
'------------------------------------------------------------------------------

'******************************************************************************
' Start/Stop MySQL as a program - Toggle button
'******************************************************************************
Function  btf_start_mysql()
  Dim safety_count,safety_count_max                    'Counter 
  Dim stop_failed

  safety_count_max  = 15                               'Time out

  bti_start_mysql.Disabled = True                     'disable button 
  bti_start_both.Disabled  = True                     'disable button 
  imgi_mysql.src="images/wait16.gif"                  'display busy image

   if bti_start_mysql.value = lang_start_mysql then   'If start button   
      us_start_mysql_program()                        'Start MySQL

     '--Saftey Loop---
     safety_count = 0                                    'Reset counter
     Do                                                  'Wait for MySQL to start
       us_hta_delay("1000")                              'Delay in ms
       safety_count = safety_count +1                    'Increment
       If safety_count >= safety_count_max Then          'Limit reached bail out                          
          bti_start_mysql.Disabled  = False              'enable button 
          bti_start_both.Disabled   = False              'enable button
          imgi_mysql.src="images/red.gif"                'display stop image
          Msgbox lang_program_str3,,lang_program_title3  'Inform user
          Exit Function                                  'Bail out
       End If
      Loop While Not us_mysql_running()                  'Check MySQL status
     '-- End Saftey Loop---


   Else                                               'Is stop button
      us_stop_mysql_program()                         'Stop MySQL
      safety_count = 0                                'reset counter
      stop_failed  = False
      '--First check
      Do                                              'Wait for MySQL to stop
         us_hta_delay("1000")                         'Delay in ms
         safety_count = safety_count + 1
         If safety_count >= safety_count_max Then     'draconian kill
           us_kill_mysql_program()
           stop_failed  = True
           Exit Do
         End If
      Loop While us_mysql_running()              'Check MySQL status
      '--Second check
      If stop_failed Then  'Wait for kill
        safety_count = 0                                'reset counter
        Do                                              'Wait for MySQL to stop
           us_hta_delay("1000")                         'Delay in ms
           safety_count = safety_count + 1
           If safety_count >= safety_count_max Then  
             Msgbox lang_program_str4,,lang_program_title4  'Inform user
             Exit Do
           End If
        Loop While us_mysql_running()              'Check MySQL status

      End If
      '-- End second
   End if

   update_status()                               'Update button status
   us_hta_delay("1000")                           'Delay in ms now update UniTray
   us_unitray_update("Refresh")                  'Check for UniTray and refresh status

  bti_start_mysql.Disabled = false               'Enable button 
  bti_start_mysql.blur                           'Remove focus and double line 

End Function
'------------------------------------------------------------------------------

'******************************************************************************
' Start/Stop Both Apache and MySQL as a program - Toggle button
'******************************************************************************
Function  btf_start_both()
  Dim safety_count,safety_count_max                 'Counter 
  Dim tim_safety_count,apache_safety_time_out       '
  Dim mysql_safety_time_out
  Dim stop_failed

  bti_start_apache.Disabled = True                  'disable button 
  bti_start_mysql.Disabled  = True                  'disable button 
  bti_start_both.Disabled   = True                  'Disable button 

  safety_count_max  = 15                            'Time out

   if bti_start_both.value = lang_start_both then   'If start button   

      '== Start Apache
      imgi_apache.src="images/wait16.gif"           'display busy image
      us_start_apache_program()                     'Start Apache

     '--Saftey Loop---
     safety_count = 0                                    'Reset counter
     apache_safety_time_out = False                      'Reset flag
     Do                                                  'Wait for Apache to start
       us_hta_delay("1000")                              'Delay in ms
       safety_count = safety_count +1                    'Increment
       If safety_count >= safety_count_max Then          'Limit reached bail out                          
          bti_start_apache.Disabled = False              'enable button 
          bti_start_mysql.Disabled  = False              'enable button 
          bti_start_both.Disabled   = False              'enable button 
          imgi_apache.src="images/red.gif"               'display stop image
          Msgbox lang_program_str1,,lang_program_title1  'Inform user
          apache_safety_time_out = True                  'Set flag
       End If
      Loop While Not us_apache_running()                 'Check Apache status
     '-- End Saftey Loop---

      If Not apache_safety_time_out Then                'Was started
        imgi_apache.src="images/green.gif"              'display started image
        us_hta_delay("2000")                            'Delay in ms
        us_redirect("start") 'Check user wants to display page. Display accordingly
      End If

      '== Start MySQL
      imgi_mysql.src="images/wait16.gif"                'display busy image
      us_start_mysql_program()                          'Start MySQL

     '--Saftey Loop---
     safety_count = 0                                    'Reset counter
     mysql_safety_time_out = False                       'Reset flag
     Do                                                  'Wait for MySQL to start
       us_hta_delay("1000")                              'Delay in ms
       safety_count = safety_count +1                    'Increment
       If safety_count >= safety_count_max Then          'Limit reached bail out                          
          bti_start_apache.Disabled = False              'enable button 
          bti_start_mysql.Disabled  = False              'enable button 
          bti_start_both.Disabled   = False              'enable button 
          imgi_mysql.src="images/red.gif"                'display stop image
          Msgbox lang_program_str3,,lang_program_title3  'Inform user
          mysql_safety_time_out = True                   'Reset flag
         update_status()                                 'Update button status 
         us_hta_delay("1000")                            'Delay in ms now update UniTray
         us_unitray_update("Refresh")                    'Check for UniTray and refresh status
         Exit Function                                   'Bail out
       End If
      Loop While Not us_mysql_running()                  'Check MySQL status
     '-- End Saftey Loop---

   Else                                             'Is stop button
      '== Apache
      imgi_apache.src="images/wait16.gif"           'display busy image
      us_stop_apache_program()                      'Stop Apache

     '--Saftey Loop---
     safety_count = 0                                    'Reset counter
     apache_safety_time_out = False                      'Reset flag
     Do                                                  'Wait for Apache to stop
       us_hta_delay("1000")                              'Delay in ms
       safety_count = safety_count +1                    'Increment
       If safety_count >= safety_count_max Then          'Limit reached bail out                          
          bti_start_apache.Disabled = False              'enable button 
          bti_start_mysql.Disabled  = False              'enable button 
          bti_start_both.Disabled   = False              'enable button 
          imgi_apache.src="images/green.gif"             'display start image
          Msgbox lang_program_str2,,lang_program_title2  'Inform user
          apache_safety_time_out = True                  'Set flag
       End If
      Loop While us_apache_running()                     'Check Apache status
     '-- End Saftey Loop---

      If Not apache_safety_time_out Then                'Was stopped
        imgi_apache.src="images/red.gif"                'display stop image
      End If

      '== MySQL
      imgi_mysql.src="images/wait16.gif"            'display busy image
      us_stop_mysql_program()                         'Stop MySQL
      safety_count = 0                                'reset counter
      stop_failed  = False
      '--First check
      Do                                              'Wait for MySQL to stop
         us_hta_delay("1000")                         'Delay in ms
         safety_count = safety_count + 1
         If safety_count >= safety_count_max Then     'draconian kill
           us_kill_mysql_program()
           stop_failed  = True
           Exit Do
         End If
      Loop While us_mysql_running()              'Check MySQL status
      '--Second check
      If stop_failed Then  'Wait for kill
        safety_count = 0                                'reset counter
        Do                                              'Wait for MySQL to stop
           us_hta_delay("1000")                         'Delay in ms
           safety_count = safety_count + 1
           If safety_count >= safety_count_max Then  
             Msgbox lang_program_str4,,lang_program_title4  'Inform user
             Exit Do
           End If
        Loop While us_mysql_running()              'Check MySQL status

      End If
      '-- End second

   End if

   update_status()                                   'Update button status
   us_hta_delay("1000")                              'Delay in ms now update UniTray
   us_unitray_update("Refresh")                      'Check for UniTray and refresh status

   bti_start_apache.Disabled = false                 'Enable button 
   bti_start_mysql.Disabled  = false                 'disable button 
   bti_start_both.Disabled   = false                 'Disable button 

  bti_start_both.blur                           'Remove focus and double line 
End Function
'------------------------------------------------------------------------------

'******************************************************************************
' Server Help
'******************************************************************************
' Displays help for runnng as program

Function btf_help()
 display_html_doc_page("start_run_as_program.html")
 bti_help.blur                           'Remove focus and double line 
End Function
'------------------------------------------------------------------------------


'******************************************************************************
' Open MySQL Command Prompt
'******************************************************************************
' Opens a command window. Changes directory to MySQL binary folder
' Sets command window background colour  

Function btf_mysql_console()
  Dim objShell
  Set objShell = CreateObject("WScript.Shell")
  objShell.Run ("%comspec% /T:B0 /K title UNIFORM SERVER MySQL Command prompt && cd " & US_MYSQL_BIN & "\")
  Set objShell = Nothing 
  bti_mysql_console.blur                           'Remove focus and double line 
End Function
'------------------------------------------------------------------------------

'******************************************************************************
' Server Status
'******************************************************************************
' Displays a list of current server status

Function btf_server_status()

  Dim objShell
  Set objShell = CreateObject("WScript.Shell")
  objShell.Run (US_UNI_CON & "\scripts\Server_Status.hta")
  Set objShell = Nothing 
 bti_server_status.blur                           'Remove focus and double line 
End Function
'------------------------------------------------------------------------------

'******************************************************************************
' Server Configuration
'******************************************************************************
' Displays server configuration menu

Function btf_server_configuration()

  Dim objShell
  Set objShell = CreateObject("WScript.Shell")
  objShell.Run (US_UNI_CON & "\config_menu\main_menu.hta")
  Set objShell = Nothing 
 bti_server_configuration.blur                   'Remove focus and double line 
End Function
'------------------------------------------------------------------------------

'******************************************************************************
' Multi Servers
'******************************************************************************
' Move server ports and rename execuitabls

Function btf_multi_servers()

  'Create object,run script passing parameter "uni_con_program.hta"
  Dim objShell
  Set objShell = CreateObject("WScript.Shell")

  window.close()          'Close this application this will be restarted 
                          'by the Multi_servers.hta application

  objShell.Run (US_UNI_CON & "\scripts\Multi_servers.hta " & chr(34) & "uni_con_program.hta" & chr(34))
  Set objShell = Nothing 
End Function
'------------------------------------------------------------------------------

'******************************************************************************
' Apache Syntax Check
'******************************************************************************
' Performs Apche syntax check

Function btf_apache_syntax_check()
 us_apache_syntax_check()               'Run check result displayed in a pop-up
 bti_apache_syntax_check.blur           'Remove focus and double line 
End Function
'------------------------------------------------------------------------------


'******************************************************************************
' About
'******************************************************************************
' Displays About Information

Function btf_about()
 Dim str

 str = ""
 str = str & lang_about_popup_str1 & vbCRLF & vbCRLF
 str = str & "People who have contributed to Uniform Server:" & vbCRLF
 str = str & "Developers:" & vbCRLF
 str = str & Space(4) & "Olajide Olaolorun (olajideolaolorun)" & vbCRLF
 str = str & Space(4) & "Mike Gleaves (Ric)" & vbCRLF
 str = str & Space(4) & "Bob Strand (BobS)" & vbCRLF

 MsgBox str ,,lang_about_popup_title1

 bti_about.blur                           'Remove focus and double line 
End Function
'------------------------------------------------------------------------------

'******************************************************************************
' View www
'******************************************************************************
' View root folder www in default browser

Function btf_view_www()
 us_redirect("www")             'Open Page in browser
 bti_view_www.blur              'Remove focus and double line 
End Function
'------------------------------------------------------------------------------

'******************************************************************************
' View ssl
'******************************************************************************
' View root folder ssl in default browser

Function btf_view_ssl()
 us_redirect("ssl")               'Open Page in browser
 bti_view_ssl.blur                'Remove focus and double line 
End Function
'------------------------------------------------------------------------------

'******************************************************************************
' phpInfo()
'******************************************************************************
' View phpInfo in default browser

Function btf_view_phpinfo()
 us_redirect("php_info")            'Open Page in browser
 bti_view_phpinfo.blur              'Remove focus and double line 
End Function
'------------------------------------------------------------------------------

'******************************************************************************
' phpMyAdmin
'******************************************************************************
' View phpMyAdmin in default browser

Function btf_view_phpmyadmin()
 us_redirect("phpmyadmin")              'Open Page in browser
 bti_view_phpmyadmin.blur              'Remove focus and double line 
End Function
'------------------------------------------------------------------------------

'******************************************************************************
' Server Documentation
'******************************************************************************
' Displays HTML Server Documentation

Function btf_server_documentation()

 '--Display file in browser
    display_html_doc_page("index.html")

 bti_server_documentation.blur                    'Remove focus and double line 
End Function
'------------------------------------------------------------------------------

'******************** End Script *****************
</script>


<link rel="stylesheet" type="text/css" href="css/main_style.css" media="screen" />
<style type="text/css">
</style>

</head>
<body>

<fieldset>
<legend id="legi_start_stop_servers" >Start servers</legend>  

<table   cellspacing="1" border="0">

<tr>
<td rowspan="4" width="180" valign="bottom"><img id="logo" src="images/logo.gif" ></td>
</tr>


<tr>
<td><button   id="bti_start_apache" onclick="btf_start_apache()">Start Apache</button></td>
<td width="20"><img  id="imgi_apache" src="images/red.gif"></td>
</tr>


<tr>
<td><button   id="bti_start_mysql" onclick="btf_start_mysql()">Start MySQL</button></td>
<td><img id="imgi_mysql" src="images/green.gif" ></td>
</tr>

<tr>
<td><button   class="common_button"  id="bti_start_both" onclick="btf_start_both()"   >Start Both</button></td>
<td><button   class="bt_help" id="bti_help"   onclick="btf_help()" >!</button></td>
</tr>
</table>

</fieldset>


<fieldset>
<legend id="legi_server_utilities" >Server Utilities</legend> 

<table width="100%" border="0" cellspacing="1">

<tr>
<td width="180"><button   id="bti_mysql_console" onclick="btf_mysql_console()">MySQL Console</button></td>
<td><button  class="common_button"   id="bti_view_www" onclick="btf_view_www()">View www</button></td>
</tr>

<tr>
<td><button   id="bti_server_status" onclick="btf_server_status()">Server Status</button></td>
<td><button  class="common_button"   id="bti_view_ssl" onclick="btf_view_ssl()">View ssl</button></td>
</tr>

<tr>
<td><button   id="bti_apache_syntax_check"      onclick="btf_apache_syntax_check()" >Apache Syntax Check</button></td>
<td><button  class="common_button"   id="bti_view_phpinfo" onclick="btf_view_phpinfo()">phpInfo</button></td>
</tr>

<tr>
<td><button   id="bti_server_configuration" onclick="btf_server_configuration()">Server Configuration</button></td>
<td><button  class="common_button"   id="bti_view_phpmyadmin" onclick="btf_view_phpmyadmin()">phpMyAdmin</button></td>
</tr>

<tr>
<td><button   id="bti_multi_servers"      onclick="btf_multi_servers()" >Multi Servers</button></td>
<td><button   id="bti_server_documentation" onclick="btf_server_documentation()">Server Documentation</button></td>
</tr>

<tr>
<td>
<div class="wrapper" onmouseover="javascript:this.style.border='1px solid #0000ee';" onmouseout="javascript:this.style.border='1px solid #000000';" >
<form id="seli_form_lang">
 <select id="seli_select_lang" class="select_dd" onchange="fsel_update_lang()"> </select>
</form>
</div>
</td>
<td><button   id="bti_about"      onclick="btf_about()" >About</button></td>
</tr>

</table>
</fieldset>

 </body>
</html>
 
