<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.01//EN" "http://www.w3.org/TR/html4/strict.dtd">
<html>
 <head>

  <script type="text/jscript" >
  // Resize and hide window. This script requires placing just below head tag.
  // Note: This js is faster than vbs hence avoids flicker.
  //       Window is hidden by moving it off screen.  

   var hta_width  = 280;                                // Set Application width
   var hta_height = 220;                                // Set Application height
   window.resizeTo(hta_width,hta_height);               // Resize window
   var hta_left = (window.screen.width-hta_width)/2;    // New position centered
   var hta_top  = (window.screen.height-hta_height)/2;  // New position centered
   window.moveTo(-2000,-2000);                          // Hide window! Move off screen
   setTimeout('show(hta_left,hta_top)',100);            // Schedule (ms) show function

   function show(hta_left,hta_top) {                    // Show Window by moving 
      window.moveTo(hta_left+10,hta_top+10);            // it into view.
   }
  </script>

  <title>Restore MySQl password</title>
 
  <hta:application
   id="coral1_restore_mysql_password"
   applicationname="coral1_restore_mysql_password" 
   border="thin"
   icon='../images/us.ico'
   maximizeButton="no"
   minimizeButton="no"
   navigable="yes"
   scroll="no"
   showintaskbar="yes"
   singleinstance="yes"
   SysMenu="yes"
  />
 
  <meta http-equiv="Content-Type" content="text/html; charset=UTF-8" />

<link rel="stylesheet" type="text/css" href="../css/main_style.css" media="screen" />
<script type="text/vbscript" src="../includes/core_config_inc.vbs"></script>
<script type="text/vbscript" src="../includes/core_functions_inc.vbs"></script>
<script type="text/vbscript" src="../lang/lang.vbs"></script>

<script type="text/vbscript">
 'MPG 4-6-2011 v1.0.0
 Dim status,path,cmd,cmd1,cmd2,cmd3,cmd4,mysql_exe
 Dim tim_safety_count,idTim1,idTim2,idTim3,idTim4,idTim5

'******************************************************************************
'Window_onLoad runs any time the HTA starts up or is refreshed
'******************************************************************************
Sub Window_OnLoad

 document.title = lang_restore_mysql_password_title  'Restore MySQL Password

 us_init()             'Check servers have moved and update accordingly

 '--Set legend text
 legi_restoring_mysql.InnerHtml      = lang_restore_mysql_legend  'Restoring MySQL password 
 iStopping_kill_server.InnerHtml     = lang_restore_stopping_kill 'Stopping Server
 iStart_no_grantr.InnerHtml          = lang_restore_start_nogrant 'Restart MySQL server with no grant
 iRestore.InnerHtml                  = lang_restore_root          'Restoring password to root
 iFinal_kill_server.InnerHtml        = lang_restore_kill_no_grant 'Killing no grant MySQL server
 iRestore_initial.InnerHtml          = lang_restore_initial       'Restore initial MySQL server state

 legi_run_restore_pass.InnerHtml     = lang_restore_run_legend    'Restore Password
 bi_run_restore.value                = lang_restore_run_button    'Run restore


 'Functions are run in the following chained order. Delay allows for user feedback 
 '  stop_mysql_server()          '1)
 '  start_no_grant()             '2)
 '  restore_password()           '3)
 '  final_kill()                 '4)
 '  reset_initial_server_state() '5)

End sub

'=== Run restore ==============================================================

Function btf_run_restore()
 imgi_kill_1.src ="../images/wait16.gif"  'User feedback
 idTim1 = setTimeout("stop_mysql_server", 1000, "vbscript")
 bi_run_restore.blur                         'Remove focus and double line 
End Function
'========================================================== End Run restore ===


'=== 1) Stop MySQL server =====================================================
'---- To restore password MySQL server must be stopped 
Function stop_mysql_server()
  clearTimeout(idTim1)            'Kill timer
  status = us_get_mysql_tracker() 'Current status. Used for restarting server if required

  If us_mysql_running() And status = "service" Then 'Running as a service
    us_stop_mysql_service()                         'Stop server
  End If

  If us_mysql_running() And status = "program" Then 'Running as a program
    us_kill_mysql_program()                         'Kill MySQL program 
  End If

  '--Delay wait for MySQL to stop
  tim_safety_count = 0                          'reset counter
  Do                                            'Wait for MySQL to stop
    us_hta_delay("100")                         'Delay in ms

    tim_safety_count = tim_safety_count + 1     'Increment counter
    If tim_safety_count > 150 Then              'draconian kill
      Exit Do
    End If
  Loop While us_mysql_running()                 'Check MySQL status

  imgi_kill_1.src ="../images/green.gif"        'User feedback

'-- Update password in tracker file 
 us_set_mysql_password("root")                  'Default root

 imgi_no_grant.src ="../images/wait16.gif"  'User feedback
 idTim2 = setTimeout("start_no_grant", 1000, "vbscript")

End Function
'======================================================== Stop MySQL server ===

'=== 2) Restart MySQL server with no grant =======================================
'---- Restart MySQL server with no grant permissions (no password required)
Function start_no_grant()
  Dim objShell
  clearTimeout(idTim2)            'Kill timer
  path      = US_MYSQL_BIN        'Path to MySQL binary
  mysql_exe = us_get_mysql_exe()  'Get name 

  'Batch file commands example:
  'CD C:\UniServer\usr\local\mysql\bin
  'mysqld1.exe --defaults-file="C:\UniServer\usr\local\mysql\my.ini" &  " --bind-address=127.0.0.1"  --skip-grant-tables --user=root
  'exit

  'Build command line
  cmd1 = " &&  CD " & US_MYSQL_BIN & " && " & mysql_exe  'Absolute path to exe 
  cmd2 = " --defaults-file=" & Chr(34) & USF_MYSQL_INI & Chr(34) 
  cmd3 = " --bind-address=127.0.0.1 --skip-grant-tables --user=root && exit"  
  cmd4 = cmd1 & cmd2 & cmd3                       '* Assemble complete command

  Set objShell = CreateObject("WScript.Shell")     ' Create a shell object
  objShell.Run "cmd /T:B0 /k  title US Test " &  cmd4,0 'Keep open hidden
  Set objShell = Nothing 

  '--Delay wait for MySQL to start
  tim_safety_count = 0                          'reset counter
  Do                                            'Wait for MySQL to start
    us_hta_delay("100")                         'Delay in ms
    tim_safety_count = tim_safety_count + 1     'Increment counter
    If tim_safety_count > 150 Then              'draconian kill
      Exit Do
    End If
  Loop While Not us_mysql_running()             'Check MySQL status

  imgi_no_grant.src ="../images/green.gif"        'User feedback

  imgi_restore.src ="../images/wait16.gif"  'User feedback
  idTim3 = setTimeout("restore_password", 1000, "vbscript")

End Function
'=================================== END Restart MySQL server with no grant ===

'=== 3) Restore password ======================================================
Function restore_password()
  Dim objShell
  clearTimeout(idTim3)            'Kill timer
'--- Restore password
  'Batch file commands example:
  'CD C:\UniServer\usr\local\mysql\bin
  'mysqld1.exe < C:\UniServer\uni_con\includes\restore.sql
  'exit

  'Build command line
  cmd1 = "&& CD " & US_MYSQL_BIN & " && " & "mysql.exe < "             'Absolute path to mysqlexe 
  cmd2 = US_UNI_CON & "\includes\restore.sql" & " && exit" 
  cmd3 = cmd1 & cmd2                                '* Assemble complete command
  Set objShell = CreateObject("WScript.Shell")     ' Create a shell object
  objShell.Run "cmd /T:B0 /c  title US Test " &  cmd3   ,0,true
  Set objShell = Nothing 
  us_hta_delay("100")            'Wait 1 sec

  imgi_restore.src ="../images/green.gif"        'User feedback

  imgi_final_kill.src ="../images/wait16.gif"  'User feedback
  idTim4 = setTimeout("final_kill", 1000, "vbscript")

End Function
'===================================================== END Restore password ===

'=== 4) Final Kill ============================================================ 
Function final_kill()

  clearTimeout(idTim4)        'Kill timer
  us_kill_mysql_program()     'Kill MySQL program 

  '--Delay wait for MySQL to stop
  tim_safety_count = 0                          'reset counter
  Do                                            'Wait for MySQL to stop
    us_hta_delay("100")                         'Delay in ms
    tim_safety_count = tim_safety_count + 1     'Increment counter
    If tim_safety_count > 150 Then              'draconian kill
      Exit Do
    End If
  Loop While us_mysql_running()                 'Check MySQL status

  imgi_final_kill.src ="../images/green.gif"    'User feedback

  imgi_restore_initial.src ="../images/wait16.gif"  'User feedback
  idTim5 = setTimeout("reset_initial_server_state", 1000, "vbscript")

End Function
'=========================================================== End Final Kill ===

'=== 5) Reset initial server state ============================================
Function reset_initial_server_state()

  clearTimeout(idTim5)        'Kill timer

'=== Establish initial server state.
  If status = "service" Then 'Was running as a service
   us_start_mysql_service()  'Run service
  End If

  If status = "program" Then 'Was running as a program
   us_start_mysql_program()  'Run program
  End If

  imgi_restore_initial.src ="../images/green.gif"        'User feedback

  MsgBox lang_restore_popup_str,,lang_restore_popup_title

End Function
'=========================================== End Reset initial server state ===

'------------------------------------------------------------------------------
</script>

<style type="text/css">
td{
  font-family: verdana,sans-serif;
  font-size:11px;
}
</style>

</head>

<body>
<fieldset>
<legend id="legi_restoring_mysql" >Restoring MySQL password</legend>  

<table   cellspacing="1" border="0">

<tr>
<td id="iStopping_kill_server">Stopping MySQL server</td>
<td width="16"><img  id="imgi_kill_1" src="../images/red.gif"></td>
</tr>

<tr>
<td id="iStart_no_grantr">Restart MySQL server with no grant</td>
<td width="16"><img  id="imgi_no_grant" src="../images/red.gif"></td>
</tr>

<tr>
<td id="iRestore">Restoring password to root</td>
<td width="16"><img  id="imgi_restore" src="../images/red.gif"></td>
</tr>

<tr>
<td id="iFinal_kill_server">Killing no grant MySQL server</td>
<td width="16"><img  id="imgi_final_kill" src="../images/red.gif"></td>
</tr>

<tr>
<td id="iRestore_initial">Restore initial MySQL server state</td>
<td width="16"><img  id="imgi_restore_initial" src="../images/red.gif"></td>
</tr>

</table>
</fieldset>

<fieldset>
<legend id="legi_run_restore_pass" >Restore Password</legend>  

<table   cellspacing="1" border="0">
<tr>
<td><button id="bi_run_restore" onclick="btf_run_restore()" >Run restore</button></td>
</tr>
</table>
</fieldset>
</body>
</html>
 
