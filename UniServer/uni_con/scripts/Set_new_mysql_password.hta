<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.01//EN" "http://www.w3.org/TR/html4/strict.dtd">
<html>
 <head>

  <script type="text/jscript" >
  // Resize and hide window. This script requires placing just below head tag.
  // Note: This js is faster than vbs hence avoids flicker.
  //       Window is hidden by moving it off screen.  

   var hta_width  = 350;                                // Set Application width
   var hta_height = 180;                                // Set Application height
   window.resizeTo(hta_width,hta_height);               // Resize window
   var hta_left = (window.screen.width-hta_width)/2;    // New position centered
   var hta_top  = (window.screen.height-hta_height)/2;  // New position centered
   window.moveTo(-2000,-2000);                          // Hide window! Move off screen
   setTimeout('show(hta_left,hta_top)',100);            // Schedule (ms) show function

   function show(hta_left,hta_top) {                    // Show Window by moving 
      window.moveTo(hta_left+10,hta_top+10);            // it into view.
   }
  </script>

  <title>Set New MySQl password</title>
 
  <hta:application
   id="coral1_set_new_mysql_password"
   applicationname="coral1_set_new_mysql_password" 
   border="thin"
   icon='../images/us.ico'
   maximizeButton="no"
   minimizeButton="no"
   navigable="yes"
   scroll="no"
   showintaskbar="yes"
   singleinstance="yes"
   SysMenu="yes"
  />
 
  <meta http-equiv="Content-Type" content="text/html; charset=UTF-8" />

<link rel="stylesheet" type="text/css" href="../css/main_style.css" media="screen" />
<script type="text/vbscript" src="../includes/core_config_inc.vbs"></script>
<script type="text/vbscript" src="../includes/core_functions_inc.vbs"></script>
<script type="text/vbscript" src="../lang/lang.vbs"></script>

<script type="text/vbscript">
'MPG 26-4-2011 V1.0
 Option Explicit
 Dim status,path,cmd,cmd1,cmd2,cmd3,cmd4,mysql_exe
 Dim tim_safety_count,idTim1,idTim2,idTim3,old_password,new_mysql_password
 Dim service_not_running

'******************************************************************************
'Window_onLoad runs any time the HTA starts up or is refreshed
'******************************************************************************
Sub Window_OnLoad

 document.title = lang_new_mysql_password_title    ' New MySQL Password 
 us_init()             'Check servers have moved and update accordingly

 '--Set legend text
 legi_new_pass_mysql.InnerHtml           = lang_new_pass_mysql_legend    'Set New MySQL Password 
 iStarting_mysql_server_1.InnerHtml      = lang_new_pass_starting_1      'Starting MySQL server
 iSetting_new_password.InnerHtml         = lang_new_setting_new_password 'Setting New Password
 iRestore_initial.InnerHtml              = lang_restore_initial          'Restore initial MySQL server state

 legi_enter_new_pass_mysql.InnerHtml     = lang_new_enter_pass_mysql_legend 'Enter a new password 
  bi_change_password.value               = lang_new_change_mysql_password   'Change password

 iMySQL_password.value = us_get_mysql_password() 'Display old password

 'Functions are run in the following chained order. Delay allows for user feedback 
 '  stop_mysql_server()          '1)
 '  set_new_password()           '2)
 '  reset_initial_server_state() '3)

 service_not_running = false 'Set initial value

End sub

'=== Change password ==========================================================
Function btf_change_mysql_password()
  Dim str1,str2,str3,str4,str5,str6,str7
  Dim objFSO,objFile
  Const ForWriting = 2

  bi_change_password.blur                         'Remove focus and double line 
  new_mysql_password = iMySQL_password.value      'Get user input

  If new_mysql_password ="" Then
   MsgBox lang_new_error_str,,lang_new_error_title
   Exit Function
  End If

  '--Create new file with password
  str1 = "SET PASSWORD FOR 'root'@'127.0.0.1' = PASSWORD('" & new_mysql_password & "');" & vbCRLF
  str2 = "SET PASSWORD FOR 'pma'@'127.0.0.1'  = PASSWORD('" & new_mysql_password & "');" & vbCRLF
  str3 = "FLUSH PRIVILEGES;" & vbCRLF
  str4 = "exit;" &  vbCRLF
  str7 = str1 & str2 & str3 & str4 
  
  Set objFSO = CreateObject("Scripting.FileSystemObject")
  Set objFile = objFSO.OpenTextFile(US_UNI_CON & "\includes\set_password.sql", ForWriting,true)
  objFile.Write str7
  objFile.Close
  Set objFSO = Nothing 

  imgi_starting_1.src ="../images/wait16.gif"     'User feedback

  idTim1 = setTimeout("start_mysql_server", 1000, "vbscript")

End Function
'====================================================== End Change password ===


'=== 1) Start MySQL server ====================================================
'- To set new password MySQL server must be running 

Function start_mysql_server()
  clearTimeout(idTim1)            'Kill timer
  status = us_get_mysql_tracker() 'Current status. Used for restarting server if required

  If Not us_mysql_running() And status = "free" Then    'Run as a program
    us_start_mysql_program()                            'Start server
  End If

  If Not us_mysql_running() And status = "service" Then 'Installed as a service
    us_start_mysql_service()                            'Start as service 
    service_not_running = true                          'Rember to turn off
  End If

  '--Delay wait for MySQL to start
  tim_safety_count = 0                                  'reset counter
  Do                                                    'Wait for MySQL to start
    us_hta_delay("100")                                'Delay in ms

    tim_safety_count = tim_safety_count + 1             'Increment counter
    If tim_safety_count > 600 Then                      'Gives 60 Seconds               
      Exit Do
    End If
  Loop While Not us_mysql_running()                     'Check MySQL status

  imgi_starting_1.src ="../images/green.gif"            'User feedback

  imgi_setting_new_pass.src ="../images/wait16.gif"  'User feedback

    us_hta_delay("5000") 'Delay 5s. Server running now give it time to set-up
                         'InnoDB for first time MySQL server run. 

  idTim2 = setTimeout("set_new_password", 1000, "vbscript")

End Function
'======================================================= Start MySQL server ===

'=== 2) Set new Password ======================================================

Function set_new_password()
  Dim objShell

  clearTimeout(idTim2)            'Kill timer

  'Batch file commands example:
  'CD C:\UniServer\usr\local\mysql\bin
  'mysql.exe -h127.0.0.1 -uroot -proot < C:\UniServer\uni_con\includes\set_password.sql
  'exit

  old_password = us_get_mysql_password()                    

  'Build command line
  cmd1 = "&& CD " & US_MYSQL_BIN & " && " & "mysql.exe -h127.0.0.1 -uroot -p" & old_password & " < "
  cmd2 = US_UNI_CON & "\includes\set_password.sql" & " && exit" 
  cmd3 = cmd1 & cmd2                                      '* Assemble complete command

  Set objShell = CreateObject("WScript.Shell")            ' Create a shell object
  objShell.Run "cmd /T:B0 /c  title US Test " &  cmd3   ,0,true
  Set objShell = Nothing 

  us_hta_delay("2000")            'Wait 2 sec

  imgi_setting_new_pass.src ="../images/green.gif"        'User feedback

  imgi_restore_initial.src ="../images/wait16.gif"        'User feedback

  '--Set new password in config file
  us_set_mysql_password(new_mysql_password) 

  idTim3 = setTimeout("reset_initial_server_state", 1000, "vbscript")

End Function
'==================================================== END Set new password ===

'=== 3) Reset initial server state ============================================
Function reset_initial_server_state()

  clearTimeout(idTim3)        'Kill timer

  '=== Establish initial server state.
  If status = "free" Then 'Was not running
    us_stop_mysql_program()   'Stop server
    wait_for_stop()
  End If

  If status = "service" And service_not_running Then 'Installed as service not running
    us_stop_mysql_service()   'Stop service
    wait_for_stop()
  End If

  imgi_restore_initial.src ="../images/green.gif"        'User feedback

  MsgBox lang_new_popup_str,,lang_new_popup_title

End Function
'=========================================== End Reset initial server state ===

'=== Wait for stop ============================================================
Function wait_for_stop()

  '--Delay wait for MySQL to stop
  tim_safety_count = 0                                  'reset counter
  Do                                                    'Wait for MySQL to start
    us_hta_delay("100")                                 'Delay in ms

    tim_safety_count = tim_safety_count + 1             'Increment counter
    If tim_safety_count > 600 Then                      'Gives 60 Seconds              
      Exit Do
    End If
  Loop While us_mysql_running()                         'Check MySQL status
End Function
'======================================================== End Wait for stop ===

</script>

<style type="text/css">
td{
  font-family: verdana,sans-serif;
  font-size:11px;
}

input{
 font-size:11px;
 font-family:Verdana;
 width:100px;
 background-color: #D1D1F5;
}

</style>

</head>

<body>

<fieldset>
<legend id="legi_new_pass_mysql" >Set New MySQL Password</legend>  

<table   cellspacing="1" border="0">

<tr>
<td id="iStarting_mysql_server_1">Starting MySQL server</td>
<td width="16"><img  id="imgi_starting_1" src="../images/red.gif"></td>
</tr>

<tr>
<td id="iSetting_new_password">Setting New Password</td>
<td width="16"><img  id="imgi_setting_new_pass" src="../images/red.gif"></td>
</tr>

<tr>
<td id="iRestore_initial">Restore initial MySQL server state</td>
<td width="16"><img  id="imgi_restore_initial" src="../images/red.gif"></td>
</tr>

</table>
</fieldset>

<fieldset>
<legend id="legi_enter_new_pass_mysql" >Enter a new password</legend>  

<table   cellspacing="1" border="0">
<tr>
<td ><input  id="iMySQL_password" type="text"></td>
<td><button id="bi_change_password" onclick="btf_change_mysql_password()" >Change password</button></td>
</tr>
</table>
</fieldset>


</body>
</html>
 
