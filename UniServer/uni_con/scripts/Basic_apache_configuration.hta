<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.01//EN" "http://www.w3.org/TR/html4/strict.dtd">
<html>
 <head>

  <script type="text/jscript" >
  // Resize and hide window. This script requires placing just below head tag.
  // Note: This js is faster than vbs hence avoids flicker.
  //       Window is hidden by moving it off screen.  

   var hta_width  = 380;                                // Set Application width
   var hta_height = 270;                                // Set Application height
   window.resizeTo(hta_width,hta_height);               // Resize window
   var hta_left = (window.screen.width-hta_width)/2;    // New position centered
   var hta_top  = (window.screen.height-hta_height)/2;  // New position centered
   window.moveTo(-2000,-2000);                          // Hide window! Move off screen
   setTimeout('show(hta_left,hta_top)',100);            // Schedule (ms) show function

   function show(hta_left,hta_top) {                    // Show Window by moving 
      window.moveTo(hta_left+10,hta_top+10);                  // it into view.
   }
  </script>

  <title>Basic Apache Configuration</title>
 
  <hta:application
   id="coral1_basic_apache_configuration"
   applicationname="coral1_basic_apache_configuration" 
   border="thin"
   icon='../images/us.ico'
   maximizeButton="no"
   minimizeButton="no"
   navigable="yes"
   scroll="no"
   showintaskbar="yes"
   singleinstance="yes"
   SysMenu="yes"
  />
 
  <meta http-equiv="Content-Type" content="text/html; charset=UTF-8" />

<link rel="stylesheet" type="text/css" href="../css/main_style.css" media="screen" />
<script type="text/vbscript" src="../includes/core_config_inc.vbs"></script>
<script type="text/vbscript" src="../includes/core_functions_inc.vbs"></script>
<script type="text/vbscript" src="../lang/lang.vbs"></script>

<script type="text/vbscript">
'MPG 14-4-2011 V1.0
 Option Explicit
 Dim ServerName,ServerAdmin,DirectoryIndex,SSI,ServerSignature,Listen 'Global
 Dim old_port  'Global

'******************************************************************************
'Window_onLoad runs any time the HTA starts up or is refreshed
'******************************************************************************
Sub Window_OnLoad
 document.title = lang_ab_page_title ' Set new page title 
 us_init()                           'Check servers have moved and update accordingly
 initial_text()                      'Set button and side text from lang file
End sub

'******************************************************************************
'Set initial button text. Text from language configuration file
'Set side text from lang file
'******************************************************************************
Function initial_text()
  Dim track,file_array1,file_array,strLine

  legi_basic_apache.InnerHtml            = lang_ab_legend               'Commonly changed parameters

  td_ba_server_name.InnerHtml            = lang_ab_server_name          'Server name
  td_ba_email_address.InnerHtml          = lang_ab_admin_email          'Server Admin Email
  td_ba_directory_index.InnerHtml        = lang_ab_directory_indexs     'Directory Index Files
  td_ba_server_side_includes.InnerHtml   = lang_ab_server_side_includes 'Server Side Includes
  td_ba_server_signature.InnerHtml       = lang_ab_server_signature     'Server Signature
  td_ba_listen_port.InnerHtml            = lang_ab_listen               'Listen

  bti_ba_update_config.value             = lang_ab_update_configuration_bt 'Update Configuration"
  bti_ba_help.value                      = lang_ab_help_bt                 'Help 

 '=== Get values from default configuration file
 file_array1 = us_get_file_as_array(USF_APACHE_DEFAULT_CNF)    'Read Apache config file

  For Each strLine in file_array1                      'Scan array
     strLine = trim(strLine)                           'Clean each line

    If Instr(1,strLine,"ServerSignature") = 1 Then      'Look for string
        strLine = Replace(strLine,"ServerSignature","") 'Remove
        ServerSignature = trim(strLine)                 'Clean line     
    End If

    If Instr(1,strLine,"AddHandler server-parsed") = 1 Then      'Look for string
        strLine = Replace(strLine,"AddHandler server-parsed","") 'Remove
        SSI = trim(strLine)                                      'Clean line     
    End If

   Next '--Get next line


 '=== Get values from configuration file
  track = 0   'Track number of Servernames we want first (Main Server)not Vhost

 file_array = us_get_file_as_array(USF_APACHE_CNF)    'Read Apache config file

  For Each strLine in file_array                      'Scan array
     strLine = trim(strLine)                          'Clean each line

    If Instr(1,strLine,"ServerName") = 1 Then         'Look for string
      If Not track = 1 Then                           'Only require first one
        strLine = Replace(strLine,"ServerName","")    'Remove
        ServerName = trim(strLine)                    'Clean line     
        track = track + 1
      End If
    End If

    If Instr(1,strLine,"ServerAdmin") = 1 Then         'Look for string
        strLine = Replace(strLine,"ServerAdmin","")    'Remove
        ServerAdmin = trim(strLine)                    'Clean line     
    End If

    If Instr(1,strLine,"DirectoryIndex") = 1 Then      'Look for string
        strLine = Replace(strLine,"DirectoryIndex","") 'Remove
        DirectoryIndex = trim(strLine)                 'Clean line     
    End If

    If Instr(1,strLine,"Listen") = 1 Then               'Look for string
        strLine = Replace(strLine,"Listen","")          'Remove
        Listen = trim(strLine)                          'Clean line     
        old_port = Listen                               'Save as old port
    End If

   Next '--Get next line

   '--Display to user
   ip_ba_server_name.value          =  ServerName      'Display
   ip_ba_email_address.value        =  ServerAdmin     'Display
   ip_ba_directory_index.value      =  DirectoryIndex  'Display
   ip_ba_server_side_includes.value =  SSI             'Display
   ip_ba_server_signature.value     =  ServerSignature 'Display
   ip_ba_listen_port.value          =  Listen          'Display

End Function
'------------------------------------------------------------------------------

'=== Update Configuration file ================================================
Function btf_ba_update_config()
  Dim track,file_array1,file_array,strLine,i,str,Listen_new
  track = 0   'Track number of Servernames we want first (Main Server)not Vhost

   '--Get User Input
   ServerName      = ip_ba_server_name.value
   ServerAdmin     = ip_ba_email_address.value 
   DirectoryIndex  = ip_ba_directory_index.value
   SSI             = ip_ba_server_side_includes.value
   ServerSignature = ip_ba_server_signature.value
   Listen_new      = ip_ba_listen_port.value

 '--Create new configuration lines and save in array1
 file_array1 = us_get_file_as_array(USF_APACHE_DEFAULT_CNF)         'Read Apache config file

    For i = 0 To UBound(file_array1)                   'Scan all lines in array
    file_array1(i) = trim(file_array1(i))                          'Clean each line

    If Instr(1,file_array1(i),"AddHandler server-parsed") = 1 Then  'Look for string
        file_array1(i) = "AddHandler server-parsed " & SSI          'New Line
    End If

    If Instr(1,file_array1(i),"ServerSignature") = 1 Then      'Look for string
        file_array1(i) = "ServerSignature " & ServerSignature  'New line
    End If

   Next '--Get next line

   call us_save_array_to_file(file_array1,USF_APACHE_DEFAULT_CNF) 'Save array 


 '--Create new configuration lines and save in array
 file_array = us_get_file_as_array(USF_APACHE_CNF)    'Read Apache config file

    For i = 0 To UBound(file_array)                   'Scan all lines in array
    file_array(i) = trim(file_array(i))                          'Clean each line

    If Instr(1,file_array(i),"ServerName") = 1 Then             'Look for string
      If Not track = 1 Then                                     'Only require first one
        file_array(i) = "ServerName " & ServerName              'New line     
        track = track + 1
      End If
    End If

    If Instr(1,file_array(i),"ServerAdmin") = 1 Then         'Look for string
      file_array(i) = "ServerAdmin " & ServerAdmin           'New line
    End If

    If Instr(1,file_array(i),"DirectoryIndex") = 1 Then      'Look for string
        file_array(i) = "DirectoryIndex " & DirectoryIndex   'New line
  
    End If

    If Instr(1,file_array(i),"Listen") = 1 Then               'Look for string
        file_array(i) = "Listen " & Listen_new                'New line
        Listen = trim(file_array(i))                          'Clean line     
    End If

    'Update old port with format :80
    Dim str1,str2
    If Instr(file_array(i),":" & old_port) Then               'Look for string :80!!
      file_array(i) = Replace(file_array(i),":" & old_port,":" & Listen_new)
    End If

   Next '--Get next line

   call us_save_array_to_file(file_array,USF_APACHE_CNF) 'Save array 

 '--- Update Vhost files
 us_set_default_server_name()     'Set _default_ vhost server name in httpd-vhosts.conf 
 us_set_ssl_default_server_name() 'Set _default_ vhost server name in httpd-ssl.conf

  bti_ba_update_config.blur
End Function
'============================================ End Update Configuration file ===

'=== Help =====================================================================
Function btf_ba_help()
  MsgBox lang_ab_help_str,,lang_ab_help_title
  bti_ba_help.blur
End Function
'================================================================= End Help ===
</script>

<style type="text/css">
table{
 width:100%;
}
td{
  padding:0px; 
  font-size:11px;
  font-family:Verdana;
}

input{
 font-size:11px;
 font-family:Verdana;
 width:97%;
  height:12px;
 background-color: #D1D1F5;
}

select{
 background-color: #D1D1F5;
  font-size:11px;
  font-family:Verdana;
}

button{
 font-size:11px;
 font-family:Verdana;
}


</style>

</head>

<body>

<fieldset>
<legend id="legi_basic_apache" >Commonly changed parameters</legend> 

<table cellspacing="8">

<tr>
<td width="50%"><input  id="ip_ba_server_name" type="text"></td>
<td         id="td_ba_server_name">Server name</td>
</tr>

<tr>
<td><input  id="ip_ba_email_address" type="text"></td>
<td         id="td_ba_email_address">Server Admin Email</td>
</tr>

<tr>
<td><input  id="ip_ba_directory_index" type="text"></td>
<td         id="td_ba_directory_index">Directory Index Files</td>
</tr>

<tr>
<td><input  id="ip_ba_server_side_includes" type="text"></td>
<td         id="td_ba_server_side_includes" >Server Side Includes</td>
</tr>

<tr>
<td><input  id="ip_ba_server_signature" type="text"></td>
<td         id="td_ba_server_signature" >Server Signature</td>
</tr>

<tr>
<td><input  id="ip_ba_listen_port" type="text"></td>
<td         id="td_ba_listen_port">Listen</td>
</tr>

</table>
</fieldset>

<table border="0" cellspacing="5">
<tr>
<td width="50%"><button   id="bti_ba_update_config" onclick="btf_ba_update_config()" >Update Configuration</button> </td>
<td><button   id="bti_ba_help" onclick="btf_ba_help()" >Help</button> </td>
</tr>
</table>

</body>
</html>
 
